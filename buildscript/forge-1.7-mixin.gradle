/** Applying this file in a Forge build will imbue it with the powers of Mixin. */

repositories {
    maven {
        name = 'sponge'
        url = 'https://repo.spongepowered.org/maven/'
    }
}

def embedMixin = !project.hasProperty("nomixin");

dependencies {
    embed("com.github.GeyserMC:MCAuthLib:d9d773e5d50327c33898c65cd545a4f6ef3ba1b5")
    if(embedMixin){
        embed('org.spongepowered:mixin:0.7.11-SNAPSHOT'){
            setTransitive false
        }
    } else {
        compile('org.spongepowered:mixin:0.7.11-SNAPSHOT'){
            setTransitive false
        }
    }
}

ext.outRefMapFile = "${tasks.compileJava.temporaryDir}/${project.modid}.mixin.refmap.json"

jar {
    manifest {
        attributes (
            'MixinConfigs': "${project.modid}.mixin.json",
            'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
            'TweakOrder': 0,
            
            // If these two are not set, Forge will not detect the mod, it will only run the mixins
            'FMLCorePluginContainsFMLMod': 'true',
            'ForceLoadAsMod': 'true',
        )
    }
    
    from outRefMapFile;
}

def outSrgFile = "${tasks.compileJava.temporaryDir}/outSrg.srg"

afterEvaluate {
    tasks.compileJava.options.compilerArgs += ["-AreobfSrgFile=${tasks.reobf.srg}", "-AoutSrgFile=${outSrgFile}", "-AoutRefMapFile=${outRefMapFile}"];
}

reobf {
    addExtraSrgFile outSrgFile
}
